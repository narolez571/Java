allprojects { projects ->
    defaultTasks 'clean', 'build'

    apply plugin: 'maven'
    apply plugin: 'java'
    apply plugin: 'idea'

    buildDir = 'target'
    version = '1.0-SNAPSHOT'
    group = 'org.suggs.sandbox'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects { subprojects ->

    ext.genDir = "$buildDir/generated-src"
    sourceSets.main.java.srcDirs += genDir

    configurations { xjc }

    task xjc(dependsOn: processResources) {
        doLast {

            file(genDir).mkdirs()

            ant.taskdef(name: 'xjc',
                    classname: 'com.sun.tools.xjc.XJCTask',
                    classpath: configurations.xjc.asPath)

            sourceSets.main.output.resourcesDir.listFiles().each { File file ->
                if (file.isFile() && file.name.endsWith("xsd")) {
                    ext.fileName = file.name.split("\\.")[0]
                    ant.xjc(destdir: genDir,
                            schema: "$sourceSets.main.output.resourcesDir/${fileName}.xsd",
                            binding: "$sourceSets.main.output.resourcesDir/${fileName}.xjb")
                }
            }
        }
    }

    dependencies {
        xjc "com.sun.xml.bind:jaxb-xjc:2.2.2"

        compile "org.slf4j:slf4j-api:$slf4jVersion"

        runtime "org.slf4j:slf4j-log4j12:$slf4jVersion"
        runtime "log4j:log4j:$log4jVersion"

        testCompile "junit:junit:$junitVersion"
        testCompile "org.mockito:mockito-all:$mockitoVersion"
    }
}

project("sandbox:guice") {
    dependencies {
    }
}

project("sandbox:hibernate") {
    dependencies {
        compile "org.hibernate:hibernate-core:$hibernateVersion"

        runtime "org.springframework:spring-context:$springVersion"
        runtime "commons-dbcp:commons-dbcp:1.4"

        testCompile "org.springframework:spring-test:$springVersion"
        testCompile "org.springframework:spring-orm:$springVersion"
        testCompile "com.googlecode.multithreadedtc:multithreadedtc:1.01"
    }
}

project("sandbox:jaxb:testschemas") {

    compileJava.dependsOn xjc

}

project("sandbox:jaxb:testclient") {


    dependencies {
        compile project(":sandbox:jaxb:testschemas")
    }
}

project("sandbox:jms") {
    dependencies {
        compile "javax.jms:jms:1.1"

        runtime "org.apache.activemq:activemq-all:5.8.0"
    }
}

project("sandbox:jmx") {
    dependencies {

    }
}

project("sandbox:oddsandsods") {
    dependencies {
        compile "log4j:log4j:$log4jVersion"
    }

}

